/*
Adobe Systems Incorporated(r) Source Code License Agreement
Copyright(c) 2005 Adobe Systems Incorporated. All rights reserved.
	
Please read this Source Code License Agreement carefully before using
the source code.
	
Adobe Systems Incorporated grants to you a perpetual, worldwide, non-exclusive,
no-charge, royalty-free, irrevocable copyright license, to reproduce,
prepare derivative works of, publicly display, publicly perform, and
distribute this source code and such derivative works in source or
object code form without any attribution requirements.
	
The name "Adobe Systems Incorporated" must not be used to endorse or promote products
derived from the source code without prior written permission.
	
You agree to indemnify, hold harmless and defend Adobe Systems Incorporated from and
against any loss, damage, claims or lawsuits, including attorney's
fees that arise or result from your use or distribution of the source
code.
	
THIS SOURCE CODE IS PROVIDED "AS IS" AND "WITH ALL FAULTS", WITHOUT
ANY TECHNICAL SUPPORT OR ANY EXPRESSED OR IMPLIED WARRANTIES, INCLUDING,
BUT NOT LIMITED TO, THE IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS
FOR A PARTICULAR PURPOSE ARE DISCLAIMED. ALSO, THERE IS NO WARRANTY OF
NON-INFRINGEMENT, TITLE OR QUIET ENJOYMENT. IN NO EVENT SHALL MACROMEDIA
OR ITS SUPPLIERS BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL,
EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO,
PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS;
OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY,
WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR
OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS SOURCE CODE, EVEN IF
ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
*/

package com.adobe.webapis.facebook.methodgroups {
	
	import com.adobe.webapis.facebook.events.FacebookResultEvent;
	import com.adobe.webapis.facebook.*;
	import flash.events.Event;
	import flash.net.URLLoader;
	
		/**
		 * Broadcast as a result of the areFriends method being called
		 *
		 * The event contains the following properties
		 *	success	- Boolean indicating if the call was successful or not
		 *	data - When success is true, contains a "friends_areFriends_response" string
		 *		   When success is false, contains an "error" FacebookError instance
		 *
		 * @see #areFriends
		 * @see com.adobe.service.facebook.FacebookError
		 * @langversion ActionScript 3.0
		 * @playerversion Flash 8.5
		 * @tiptext
		 */
		[Event(name="friendsAreFriends", 
			 type="com.adobe.webapis.facebook.events.FacebookResultEvent")]
		
	/**
	 * Contains the methods for the Feed method group in the Facebook API.
	 * 
	 * Even though the events are listed here, they're really broadcast
	 * from the FacebookService instance itself to make using the service
	 * easier.
	 */
	public class Friends {
			 
		/** 
		 * A reference to the FacebookService that contains the api key
		 * and logic for processing API calls/responses
		 */
		private var _service:FacebookService;
	
		/**
		 * Construct a new Feed "method group" class
		 *
		 * @param service The FacebookService this method group
		 *		is associated with.
		 * @langversion ActionScript 3.0
		 * @playerversion Flash 8.5
		 * @tiptext
		 */
		public function Friends( service:FacebookService ) {
			_service = service;
		}
		
		/**
		 * Returns whether or not each pair of specified users is friends with 
		 * each other. The first array specifies one half of each pair, the 
		 * second array the other half; therefore, they must be of equal size.
		 * 
		 * @param uids1 A list of user ids matched with uids2.
		 * @param uids2 A list of user ids matched with uids1.
		 * @see http://developers.facebook.com/documentation.php?v=1.0&doc=friends
		 * @langversion ActionScript 3.0
		 * @playerversion Flash 8.5
		 * @tiptext
		 */
		public function areFriends( uids1:Array, uids2:Array ):void {
			// Let the Helper do the work to invoke the method			
			MethodGroupHelper.invokeMethod( _service, areFriends_result, 
									"facebook.friends.areFriends", 
									true,
									new NameValuePair( "uids1", uids1.toString() ),
									new NameValuePair( "uids2", uids2.toString() )
									);
		}
		
		/**
		 * Capture the result of the areFriends call, and dispatch
		 * the event to anyone listening.
		 *
		 * @param event The complete event generated by the URLLoader
		 * 			that was used to communicate with the Facebook API
		 *			from the invokeMethod method in MethodGroupHelper
		 */
		private function areFriends_result( event:flash.events.Event ):void {
			// Create an FRIENDS_ARE_FRIENDS event
			var result:FacebookResultEvent = new FacebookResultEvent( FacebookResultEvent.FRIENDS_ARE_FRIENDS );

			// Have the Helper handle parsing the result from the server - get the data
			// from the URLLoader which correspondes to the result from the API call
			MethodGroupHelper.processAndDispatch( _service, 
												  URLLoader( event.target ).data, 
												  result,
												  "friends_areFriends_response",
												  MethodGroupHelper.parseFriendsAreFriends );
		}
		
		/**
		 * Returns the identifiers of the current user's Facebook friends. 
		 * The current user is determined from the session_key parameter. 
		 * The values returned from this call are not storable.
		 * 
		 * @see http://developers.facebook.com/documentation.php?v=1.0&doc=friends
		 * @langversion ActionScript 3.0
		 * @playerversion Flash 8.5
		 * @tiptext
		 */
		public function get():void {
			// Let the Helper do the work to invoke the method			
			MethodGroupHelper.invokeMethod( _service, get_result, "facebook.friends.get", true );
		}
		
		/**
		 * Capture the result of the get call, and dispatch
		 * the event to anyone listening.
		 *
		 * @param event The complete event generated by the URLLoader
		 * 			that was used to communicate with the Facebook API
		 *			from the invokeMethod method in MethodGroupHelper
		 */
		private function get_result( event:flash.events.Event ):void {
			// Create an FRIENDS_GET event
			var result:FacebookResultEvent = new FacebookResultEvent( FacebookResultEvent.FRIENDS_GET );

			// Have the Helper handle parsing the result from the server - get the data
			// from the URLLoader which correspondes to the result from the API call
			MethodGroupHelper.processAndDispatch( _service, 
												  URLLoader( event.target ).data, 
												  result,
												  "friends_get_response",
												  MethodGroupHelper.parseFriendsGet );
		}
		
		/**
		 * Returns the identifiers of the current user's Facebook friends who are signed 
		 * up for the specific calling application. The current user is determined from 
		 * the session_key parameter. 
		 * The values returned from this call are not storable.
		 * 
		 * @see http://developers.facebook.com/documentation.php?v=1.0&doc=friends
		 * @langversion ActionScript 3.0
		 * @playerversion Flash 8.5
		 * @tiptext
		 */
		public function getAppUsers():void {
			// Let the Helper do the work to invoke the method			
			MethodGroupHelper.invokeMethod( _service, get_result, "facebook.friends.getAppUsers", true );
		}
		
		/**
		 * Capture the result of the getAppUsers call, and dispatch
		 * the event to anyone listening.
		 *
		 * @param event The complete event generated by the URLLoader
		 * 			that was used to communicate with the Facebook API
		 *			from the invokeMethod method in MethodGroupHelper
		 */
		private function getAppUsers_result( event:flash.events.Event ):void {
			// Create an FRIENDS_GET_APP_USERS event
			var result:FacebookResultEvent = new FacebookResultEvent( FacebookResultEvent.FRIENDS_GET_APP_USERS );

			// Have the Helper handle parsing the result from the server - get the data
			// from the URLLoader which correspondes to the result from the API call
			MethodGroupHelper.processAndDispatch( _service, 
												  URLLoader( event.target ).data, 
												  result,
												  "friends_getAppUsers_response",
												  MethodGroupHelper.parseFriendsGetAppUsers );
		}
		
	}	
	
}